plugins {
	id 'fabric-loom' version '1.4-SNAPSHOT'
	id 'com.diffplug.spotless' version '6.19.0'
	id "com.modrinth.minotaur" version "2.+"
}

java {
	sourceCompatibility = JavaVersion.VERSION_18
	targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	maven { url 'https://masa.dy.fi/maven' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
	maven { url 'https://jitpack.io' }
	flatDir {
		dirs '.'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.mappings_version}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
	modImplementation "fi.dy.masa.malilib:malilib-fabric-${project.minecraft_version_out}:${project.malilib_version}"
	modCompileOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

group = project.group + "." + project.mod_id
base {
	archivesName = project.mod_file_name + '-' + project.minecraft_version_out
}
version = project.mod_version

if (version.endsWith('-dev')) {
	version += "." + new Date().format('yyyyMMdd.HHmmss')
}

processResources {
	inputs.property "mod_version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand([
				"mod_version"          : project.version,
				"minecraft_version_min": project.property("minecraft_version_min"),
				"malilib_version"   : project.property("malilib_version")
		])
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		cleanthat()
		eclipse()
		formatAnnotations()
	}
}

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	syncBodyFrom = rootProject.file("README.md").text
	projectId = 'C1naQCmt'
	uploadFile = remapJar
	gameVersions = ['1.20.3','1.20.4']
	loaders = ['fabric']
	dependencies = []
}
